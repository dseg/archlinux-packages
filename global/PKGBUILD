# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien orontee@gmail.com

pkgname=global
pkgver=6.3.3
pkgrel=1
pkgdesc="A source code tag system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/global/"
license=('GPL')
depends=('libltdl' 'bash' 'perl')
optdepends=('idutils' 'ctags' 'python2-pygments')
options=(!emptydirs !libtool)
install=global.install
source=(http://tamacom.com/global/${pkgname}-${pkgver}.tar.gz
fix-gozilla-makefile.patch)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/\.la/.so/g' gtags.conf.in
	patch -Np0 < $srcdir/fix-gozilla-makefile.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure --prefix=/usr --with-exuberant-ctags=/usr/bin/ctags
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -k check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/emacs/site-lisp"
	ln -s ../../gtags/gtags.el "${pkgdir}/usr/share/emacs/site-lisp/gtags.el"
}

md5sums=('bef48c9e6bd7aa78eef984932df1bc6c'
         '41cc0ea1a3866c672f7861101eb2a387')
sha256sums=('dd05090c0ab1e41cb79f60796b760a81ca33af4486b3707a3ec749265e430249'
            'c146d8be2c4e793175dce2b9dc266b5a1704ae7025a414d7cade062573f01441')
